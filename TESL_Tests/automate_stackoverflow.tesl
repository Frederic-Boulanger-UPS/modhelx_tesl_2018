// This model should trigger an error because there is an
// unsupported dependency between the start clock enter1 of a 
// sustained implication of in1 and in1 itself
 
// events
int-clock eventA sporadic 4, 20
int-clock eventB sporadic 5, 11, 23
int-clock eventC sporadic 25

// clock of all events
int-clock allEvents
eventA implies allEvents
eventB implies allEvents
eventC implies allEvents

tag relation eventA = allEvents
tag relation eventB = allEvents
tag relation eventC = allEvents

// start signal (used to switch to the initial state)
int-clock start sporadic 0
tag relation start = allEvents

// states: generic enter/in/leave events
unit-clock enter1
unit-clock enter2
unit-clock enter3

unit-clock in1
unit-clock in2
unit-clock in3

unit-clock leave1
unit-clock leave2
unit-clock leave3

enter2 when in1 implies leave1
enter1 when in2 implies leave2
enter3 when in2 implies leave2
enter1 when in3 implies leave3

allEvents sustained immediately from enter1 to leave1 implies in1
allEvents sustained immediately from enter2 to leave2 implies in2
allEvents sustained immediately from enter3 to leave3 implies in3

// timed events (for timed transitions)
unit-clock eventTimeout2to3
in2 time delayed by 2 on allEvents implies eventTimeout2to3

// transition table
in1 when eventA implies enter2
in2 when eventB implies enter1
in2 when eventTimeout2to3 implies enter3
in3 when eventC implies enter1

// stop
int-clock end sporadic 30
tag relation end = allEvents
@stop when end
@maxstep 100
